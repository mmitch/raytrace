defaultTasks 'check'

apply plugin: 'java'
/* we are a Java project, obviously */
sourceCompatibility = '1.8'


apply plugin: 'eclipse'
/* generate eclipse project, disable compiler warnings on tests */
eclipse {
	classpath {
		file {
			whenMerged { classpath ->
			 	classpath.entries.findAll{entry -> entry.kind == 'src' && entry.path == 'src/test/java'}.each{
			 		entry -> entry.entryAttributes['ignore_optional_problems'] = true
			 	}
			}
		}
	}
}


apply plugin: 'jacoco'
/* generate Java code coverage reports */
jacocoTestReport {
	reports {
		xml.enabled true
		html.enabled true
	}

        /* exclude RayTrace class from test coverage, it's just a quick demo */
        afterEvaluate {
                classDirectories = files(classDirectories.files.collect {
                        fileTree(dir: it, exclude: [
                                '**/RayTrace.class'
                        ])
                })
        }
}


apply plugin: 'com.cinnober.gradle.semver-git'
/* deduce build version number from git tags
   source: https://github.com/cinnober/semver-git */
buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath group: 'com.cinnober.gradle', name: 'semver-git', version: '2.2.1'
	}
}


repositories {
    mavenCentral()
    repositories {
        maven {
			name "cgarbs.de repo"
			url "https://www.cgarbs.de/maven2"
		}
	}
}

dependencies {
    compile 'de.cgarbs:wavefront-simple:0.5.0'
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
}

task packageSources(type: Jar, dependsOn: 'classes') {
	from sourceSets.main.allSource
	classifier = 'sources'
}
task packageJavadoc(type: Jar, dependsOn: 'javadoc') {
	from javadoc
	classifier = 'javadoc'
}

 /***
 **** my own tasks
 ***/

task version() {
     description 'Show version that would be published'

     println 'version=' + version
}
